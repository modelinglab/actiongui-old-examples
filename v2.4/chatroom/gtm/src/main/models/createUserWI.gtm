Window createUserWI{
    Label nicknameLA{
        String text := ['Nickname:']
    }
    TextField nicknameTF{
        String text := ['']
    }
    Label loginLA{
        String text := ['Login:']
    }
    TextField loginTF{
        String text := ['']
    }
    Label passwordLA{
        String text := ['Password:']
    }
    TextField passwordTF{
        String text := ['']
    }
    Label rolesLA{
        String text := ['Select a role:']
    }
    ComboBox rolesCB{
        Set(Role) rows := [Role.allInstances()->select(r|r <> Role::ANONYMOUS)]
        
        Label role{
            String text := [$rolesCB.row$.name()]
        }
    }
    Button createUserBU{
        String text := ['Create']
    }
    Button backBU{
        String text := ['Back']
    }
}

Button createUserWI.createUserBU{
    event onClick{
        if [$rolesCB.selected$.oclIsUndefined()]{
            notification(['Message'], ['Please, select a role'],[2000])
        }
        else{
            if [User.allInstances()->exists(u|u.login=$loginTF.text$)] {
                notification(['Message'], ['There exists a registered user with this login. Please, choose a different login.'], [2000])
                reevaluate loginTF.text
            }
            else {
                if [User.allInstances()->exists(u|u.nickname=$nicknameTF.text$)] {
                    notification(['Message'], ['There exists a registered user with this nickname. Please, choose a different nickname.'], [2000])
                    reevaluate nicknameTF.text
                }
                else {
                    newUser := new User
                    [$newUser$.nickname] := [$nicknameTF.text$]
                    [$newUser$.login] := [$loginTF.text$]
                    [$newUser$.password] := [$passwordTF.text$]
                    [$newUser$.role] := [$rolesCB.selected$]
                    notification(['The user has been created'],[''],[2000])
                    reevaluate nicknameTF.text
                    reevaluate  passwordTF.text
                    reevaluate loginTF.text
                }
            }
        }
    }
}
Button createUserWI.backBU{
    event onClick{
        back
    }
}