Window ListDinersWI {
    String text := ['Meal list']
    Residence residence
    ZoneId zone := [$residence$.zone]
    Label DateLA {
        String text := ['Choose a date:']
    }

    DateField DateDF {
        LocalDateTime value :=  [LocalDateTime.now($ListDinersWI.zone$)]

        event onChange(value) {
            #refresh DinersTA#
        }
    }

    Label MealLA {
        String text := ['Choose a meal:']
    }

    ComboBox MealCB {
        OrderedSet(Meal) rows := [$ListDinersWI.residence$.meals->sortedBy(position)]
        
        Label Meal {
            String text := [$MealCB.row$.literal]
        }

        event onChange(selected) {
            reevaluate MealOptionCB.rows
        }
    }

    Label MealOptionLA {
        String text := ['Choose a meal option:']
    }

    ComboBox MealOptionCB {
        OrderedSet(MealOption) rows

        Label MealOption {
            String text := [$MealOptionCB.row$.literal]
        }

        event onView(rows) {
            if [$MealCB.selected$.oclIsUndefined()] {
                rows := [MealOption.allInstances()->select(false)->asOrderedSet()]
            }
            else {
                rows := [$MealCB.selected$.options->sortedBy(position)]
            }
        }

        event onChange(selected) {
            #refresh DinersTA#
        }
    }

    Label DinersLA {
        String text := ['Diners for the selected data:']
    }

    #table DinersTA {
        entity : User
        rows : [(if $MealOptionCB.selected$.oclIsUndefined() or $DateDF.value$.oclIsUndefined() then User.allInstances()->select(false) else $ListDinersWI.residence$.users->select(u|u.dailyMeals->exists(dms| dms.date.toLocalDate().atStartOfDay() = $DateDF.value$.toLocalDate().atStartOfDay() and dms.selections->exists(ms| ms.mealOption = $MealOptionCB.selected$))) endif)]
        column : name {
            label : ['Name']
        }
        column : surname {
            label : ['Surname']
        }
        size: [10]
        sortedBy: [name]
    }#

    Button BackBU {
        String text := ['Back']

        event onClick {
            back
        }
    }
}
