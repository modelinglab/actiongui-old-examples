role ADMINISTRATIVE extends DEFAULT{
    Professional{
        // intersection is not supported
        // read constrainedBy [caller.worksIn->intersection(self.worksIn)->notEmpty()]
        read constrainedBy [caller.worksIn->exists(m| self.worksIn->includes(m))]
    }
    MedicalInfo{
        create
    }
    Patient{
        create
        add (social), add (allocation), add (medical), add (administrative), add (contact),
            add (public), update (vip) constrainedBy [caller.worksIn->includes(self.department.belongsTo->any(true))]
        add (department), remove (department) constrainedBy [caller.worksIn->includes(target.belongsTo->any(true))]
        read constrainedBy [caller.worksIn->includes(self.department.belongsTo->any(true)) or self.department->isEmpty()]
        // intersection not supported
        // add doctor, remove doctor constrainedBy [caller.worksIn->intersection(target.professional.worksIn)->notEmpty()]
        add (doctor), remove (doctor) constrainedBy [caller.worksIn->exists(m| target.professional.worksIn->includes(m))]
    }
    Expense{
        // fullAccess constrainedBy [caller.worksIn->includes(self.administrative.patient.department.belongsTo->any(true))]
        create
        delete, read, update constrainedBy [caller.worksIn->includes(self.administrative.patient.department.belongsTo->any(true))]
    }
    ContactInfo{
        create
        read, update constrainedBy [caller.worksIn->includes(self.patient.department.belongsTo->any(true))]
    }
    PublicInfo{
        read, update constrainedBy [caller.worksIn->includes(self.patient.department.belongsTo->any(true))]
        create
    }
    AdministrativeInfo{
        create
        read, update constrainedBy [caller.worksIn->includes(self.patient.department.belongsTo->any(true))]
    }
    AllocationInfo{
        read, update constrainedBy [caller.worksIn->includes(self.patient.department.belongsTo->any(true))]
        create
    }
    SocialInfo{
        create
        update (description) constrainedBy [caller.worksIn->includes(self.patient.department.belongsTo->any(true))]
    }
    Doctor {
        read constrainedBy [caller.worksIn->exists(m| self.professional.worksIn->includes(m))]
    }
}