Window Login_W isMain{
    String text := ['Log In']

//
    #table Meals_T {
        entity : Meal
        column : literal {
                    label : ['Meal']
                }
        size: [10]
        sortedBy : [literal]
    }#

    #table Meal_Options_T {
        entity : MealOption
        rows : [$Meals_T.selected$.options]
        column : literal {
                    label : ['Option']
                    }
        size: [10]
        sortedby : [literal]
    }#

    #table Meal_Option_Deadlines_T {
        entity : MealOptionDeadline
        rows : [$Meal_Options_T.selected$.deadlines]
        column : cday {
                    label : ['Day']
                    }
        column : chour {
                    label :['Hour']
                    }
        column : cminute {
                    label : ['Minute']
                }
    }#

//

    #form Credentials_F {
        entity: Employee
        attr: login {
            label: ['Login:']
        }
        attr: password {
            label: ['Passw:']
        }
    }#

    Button Login_B {
        String text := ['Log in']
        Employee loggedUser
        event onClick {
            #login {
                credentials: Credentials_F
                result: loggedUser
            }#
            if [not($loggedUser$.oclIsUndefined())] {
                if [$loggedUser$.myRole = Role::SALESPERSON] {
                    open SalesPersonMenu_W(caller:[$loggedUser$], role:[$loggedUser$.myRole])
                }
                else {
                    if [$loggedUser$.myRole = Role::MANAGER] {
                        open ManagerMenu_W(caller:[$loggedUser$], role:[$loggedUser$.myRole])
                    }
                    else {
                        notification (['Message'],['You have no role assigned.'],[-1])
                    }
                } 
            }
            else {
                notification (['Message'],['Wrong credentials.'],[-1])
            }
        }
    }
}