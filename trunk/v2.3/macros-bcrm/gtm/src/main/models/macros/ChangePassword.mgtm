Window ChangePassword_W {
    String text := ['Change Password']

    Label oldPassword_L {
        String text := ['Current password:']
    }
    PasswordField oldPassword {
        String text := [null]
        String error := [null]
    }

    Label newPassword_L {
        String text := ['New password:']
    }
    PasswordField newPassword {
        String text := [null]
        String error := [null]
    }

    Label newPassword2_L {
        String text := ['Repeat new password:']
    }
    PasswordField newPassword2 {
        String text := [null]
        String error := [null]
    }

    Button change_B {
        String text := ['Change']

        event onClick {
            reevaluate oldPassword.error
            reevaluate newPassword.error
            reevaluate newPassword2.error

            if [$oldPassword.text$.oclIsUndefined()] {
                oldPassword.error := ['Required field']
            }
            else {
                if [$ChangePassword_W.caller$.password <> $oldPassword.text$] {
                    oldPassword.error := ['Wrong password']
                }
            }
            if [$newPassword.text$.oclIsUndefined()] {
                newPassword.error := ['Required field']
            }
            if [$newPassword2.text$.oclIsUndefined()] {
                newPassword2.error := ['Required field']
            }
            else {
                if [not $newPassword.text$.oclIsUndefined()] {
                    if [$newPassword.text$ <> $newPassword2.text$] {
                        newPassword2.error := ['The passwords do not match']
                    }
                }
            }
            
            if [$oldPassword.error$.oclIsUndefined() and $newPassword.error$.oclIsUndefined() and $newPassword2.error$.oclIsUndefined()] {
                [$ChangePassword_W.caller$.password] := [$newPassword.text$]

                reevaluate oldPassword.text
                reevaluate newPassword.text
                reevaluate newPassword2.text
                
                notification (['Success'],['Password updated successfully.'],[-1])
            }
            else {
                notification (['Error'],['The form contains error. Please, check the form.'],[-1])
            }
        }
    }

    Button back_B {
        String text := ['Back']
        
        event onClick {
            back
        }   
    }
}