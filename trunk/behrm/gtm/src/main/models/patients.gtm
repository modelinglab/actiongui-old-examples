Window patientsWi{
    String text := ['Patients']
    MedicalCenter medicalCenter

    Label medicalCenterLa{
        String text := ['Medical center: '.concat($patientsWi.medicalCenter$.name)]
    }
    Label patientsLa{
        String text := ['List of patients:']
    }
    Table patientsTa{
        Set(Patient) rows := [$patientsWi.medicalCenter$.departments.patients->asSet()]
    }
    Button movePatientBu{
        String text := ['Move the selected patient']
    }
    Button backBu{
        String text := ['Back']
    }
}

Table patientsWi.patientsTa{
    columns {
        ['Name'] : Label nameLa {
            String text := [$patientsTa.row$.contact->any(true).name]
        }
        ['Surname'] : Label surnameLa {
            String text := [$patientsTa.row$.contact->any(true).surname]
        }
        ['Gender'] : Label genderLa {
            String text := [$patientsTa.row$.contact->any(true).gender]
        }
        ['Floor'] : Label floorLa {
            String text := [$patientsTa.row$.allocation->any(true).floor]
        }
        ['Room'] : Label roomLa {
            String text := [$patientsTa.row$.allocation->any(true).room]
        }
        ['Social Info'] : Label socialInfoLa {
            String text := [$patientsTa.row$.social->any(true).description]
        }
    }
}

Button patientsWi.movePatientBu{
    event onClick{
        if [$patientsWi.caller$.asAdministrative->notEmpty()] {
            if [not $patientsTa.selected$.oclIsUndefined()] {
                open movePatientWi(medicalCenter:[$patientsWi.medicalCenter$], patient: [$patientsTa.selected$])
            }
            else{
                notification(['Message'],['You have to select a patient.'],[-1])
            }
        }
        else{
            notification(['Message'],['You are not administrative of this medical center.'],[-1])
        }
    }
}

Button patientsWi.backBu{
    event onClick{
        back
    }
}

