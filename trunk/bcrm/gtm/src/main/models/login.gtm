Window loginWI isMain{
    String text := ['Basic CRM App']

    Label loginLA {
        String text := ['User:']
    }

    TextField loginTF {
        String text := ['']
    }

    Label passLA {
        String text := ['Password:']
    }

    TextField passTF {
        String text := ['']
    }

    Button loginBU {
        String text := ['Login']

        event onClick {
            if [Employee.allInstances()->exists(u|u.login = $loginTF.text$ and u.password = $passTF.text$)] {
                loginWI.caller := [Employee.allInstances()->any(u|u.login = $loginTF.text$ and u.password = $passTF.text$)]
                // exactly one role for each user
                loginWI.role := [$loginWI.caller$.roles->any(true)]
                if [$loginWI.role$.role = 'Manager'] {
                    open managerMenuWI(caller : [$loginWI.caller$], role : [$loginWI.role$])
                }
                else {
                    if [$loginWI.role$.role = 'SalesPerson'] {
                        open salesPersonMenuWI(caller : [$loginWI.caller$], role : [$loginWI.role$])
                    }
                }
            }
            else {
                notification(['Error'],['There is no any user with these credentials'],[-1])
            }
        }
    }

    Button backBU {
        String text := ['Exit']

        event onClick {
            exit
        }
    }
}